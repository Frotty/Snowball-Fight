package KillStreaks
import PlayerData
import GameConstants
import Gamemode
import ClosureTimers
import Entity
import initlater Child

public function adjustKills(player killer, player dead, LinkedList<Child> enemyChildren)
	var p1 = pData[killer.getId()]
	var p2 = pData[dead.getId()]
	if p1 == p2
		DisplayTimedTextToForce(GetPlayersAll(), 12., p1.p.getNameColored() + " killed himself! (|cffff0000-1|r)")
		p1.killstreak = 0
		p1.deaths++
		if p1.id <= 4
			humanscore -= 1
		else
			orcscore -= 1
		return
	p1.kills++
	p2.deaths++
	playerboard.updateValue(p1.gameId + 1, 1, p1.kills.toString())
	playerboard.updateValue(p2.gameId + 1, 2, p2.deaths.toString())
	killer.addGold(10)
	p1.killstreak++
	p2.killstreak = 0
	if p1.id <= 4
		if humanscore < winscore - 1
			humanscore += 1
	else
		if orcscore < winscore - 1
			orcscore += 1

	let aliveEnemies = enemyChildren.filter(child -> child.actor.isAlive())
	string announce = " -"
	if enemyChildren.size() > 1 and aliveEnemies.size() == 0
		announcers[9].play()
		announce += " |cff008669Team wiped!|r"


	if p1.killstreak == 3
		announcers[3].play()
		announce += " |cff860B00KillingSpree|r"
	if p1.killstreak == 4
		announcers[4].play()
		announce += " |cff0058A6Unstoppable!|r"
	if p1.killstreak == 6
		announcers[6].play()
		announce += " |cffFFDA84Godlike!!!|r"
	if p1.killstreak == 7
		p1.child.addExclamationMark()
	if p1.killstreak > 6
		announcers[8].play()
		announce += " |cff11DA84Beyond Godlike!!!|r"
	let t = getTimer()
	if p1.intriple
		announcers[1].play()
		announce += " |cff2DCB2DTriplekill|r"
		p1.indouble = false
		t..setData(killer.getId())..start(0.,function clearKillMultiplier)
	else if p1.indouble
		announcers[0].play()
		announce += " |cff2DCB2DDoublekill|r"
		p1.intriple = true
		t..setData(killer.getId())..start(15,function clearKillMultiplier)
	else
		t..setData(killer.getId())..start(20,function clearKillMultiplier)
		p1.indouble = true
	if announce != " -"
		DisplayTimedTextToForce(GetPlayersAll(), 11., killer.getNameColored() + " hit " + dead.getNameColored() + "|r in the face with a snowball!" + announce + " (|cffFFCC00+1|r)")
	else
		DisplayTimedTextToForce(GetPlayersAll(), 11., killer.getNameColored() + " hit " + dead.getNameColored() + "|r in the face with a snowball! (|cffFFCC00+1|r)")

	doAfter(2.5) ->
		checkEnd()


function clearKillMultiplier()
	var p = pData[GetExpiredTimer().getData()]
	if p.indouble
		p.indouble = false
	else
		p.intriple = false
