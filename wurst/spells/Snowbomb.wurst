package Snowbomb
import Projectile
import Fx
import initlater Child
import AbilityTooltipGenerator
import Assets
import InstantDummyCaster
import Orders

constant SPEED = 13.5
constant RealLevelClosure AOE = lvl -> (235. + lvl * 35)
constant RealLevelClosure FACTOR = lvl -> 6.95 + (lvl * 0.5)

public constant STUN_SPELL_ID = compiletime(ABIL_ID_GEN.next())
public constant SNOWBOMB_SPELL_ID = compiletime(ABIL_ID_GEN.next())

public class Snowbomb extends PhysicsProjectile
	var lvl = 1

	construct(vec3 pos, player owner, vec3 target, int lvl)
		super(pos, 16., owner, pos.angleTo2d(target), "war3mapImported\\FrostMissile.mdx")
		this.lvl = lvl
		let dist = target.distanceToSq(pos)
		if dist < 512. * 512.
			setTarget(target, max(SPEED * (dist / (512. .squared())), 2))
		else
			setTarget(target, SPEED)

	override function onGroundHit()
		addEffect("war3mapImported\\IceSparks.mdx", pos)..setScale(1.5).destr()
		for c in getEnemyChildren(owner)
			if pos.distanceToSq(c.getPos()) < AOE.run(lvl).squared()
				let angle = pos.angleTo2d(c.pos)
				let factor = 0.1 + (AOE.run(lvl) - pos.distanceTo(c.pos)) / 200.
				c.addVel(angle.toVec(factor * FACTOR.run(lvl)).withZ(14 * factor))
				fx.getDummy().damageTarget(c.actor, (50 + (lvl * 10)) * factor , ATTACK_TYPE_SIEGE)
				flashEffect(Objects.nPCbloodVillagerWoman, c.actor, "origin")
				if factor > 0.175
					InstantDummyCaster.castTarget(owner, STUN_SPELL_ID, 1, OrderIds.creepthunderbolt, c.actor, c.pos)

		terminate()


@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("High explosive. Damage and Knockback in AoE. Has minimum distance!")
	new ChannelAbilityPreset(SNOWBOMB_SPELL_ID, 4, true, tgen)
	..presetOption(Option.VISIBLE, true)
	..presetOption(Option.TARGETIMAGE, true)
	..presetButtonPosNormal(2, 2)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("E")
	..setName("Snowbomb")
	..presetIcon("BTNCRFrostShock")
	..presetCooldown(lvl -> 17. - lvl)
	..presetManaCost(lvl -> 6 - (lvl div 3))
	..presetCastRange(lvl -> 700. + 200 * lvl)
	..presetAreaofEffect(AOE)
	..tooltipStopListen()

	new AbilityDefinitionThunderBoltCreep(STUN_SPELL_ID)
	..presetDamage(lvl -> 0)
	..presetManaCost(lvl -> 0)
	..presetCooldown(lvl -> 0)
	..presetDurationHero(lvl -> 0.5)
	..presetCastRange(lvl -> 10000)
