package Snowman
import HomingProjectile
import Child
import SoundUtils
import FxEntity
import Collision
import Assets
import AbilityTooltipGenerator

constant real SPEED = 9.25
constant real MELT = 92

public constant SNOWMAN_SPELL_ID = compiletime(ABIL_ID_GEN.next())

constant throwSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltLaunch1.wav", false, false)

public class SnowmanMissile extends HomingProjectile
	var level = 1

	construct(vec3 pos, player owner, vec2 target, int level)
		super(pos, 28., owner, pos.angleTo2d(target), "Magic Missile.mdx")
		this.level = level
		fx.setScale(.75)
		setTimed(6.5)
		setSpeed(SPEED)
		throwSound.playOnPoint(pos)
		findTarget(target)
		fx.setColor(owner.getColor().toColor().withAlpha(255))
		registerCollider(this, SHAPE_SPHERE, (e) -> onHit(e))

	function findTarget(vec2 target)
		while this.target == null
			var closestDist = 2147483640.
			Child closestChild = null
			for c in getEnemyChildren(owner)
				if c.owner.isEnemyOf(owner)
					let dist = target.distanceToSq(c.pos.toVec2())
					if dist < closestDist
						closestDist = dist
						closestChild = c
			setTarget(closestChild, HOMING.angular, 0.025 + 0.005 * level)

	override function onHit()
		onHit(target)

	function onHit(Entity e)
		if e != null and not e.done and e instanceof Child and e.owner.isEnemyOf(owner)
			let child = e castTo Child
			if child.snowman == null
				new SnowmanDummy(child.getPos().add(0,0,-child.terrainZ), owner, child)
			else
				child.snowman.melttime = MELT + 10 * level
			terminate()

	ondestroy
		removeCollider(this)


public class SnowmanDummy extends FxEntity
	real melttime = MELT
	Child target

	construct(vec3 pos, player owner, Child target )
		super(owner, pos, 0., target.actor.getFacingAngle(), Doodads.snowMan)
		fx.setScale(1.4)
		addEffect(Abilities.polyMorphTarget, pos).destr()
		this.target = target
		target.actor.pause()
		target.snowman = this
		target.actor.setVertexColor(155,155,255,255)
		SetUnitTimeScale(target.actor, 0)
		silent = true

	override function update()
		melttime--
		fx..setColor(colorA(255, 255, 255, (melttime * 1.5).toInt() + 48))

		if target.actor.getHP() > .405
			setPos(target.pos)
			if target.vel.z > 0.
				target.vel.z *= 0.9
			fx.getDummy().damageTarget(target.actor, .175, ATTACK_TYPE_MAGIC)
			if melttime < 0
				terminate()
		else
			terminate()


	ondestroy
		target.actor.unpause()
		target.actor.setVertexColor(COLOR_WHITE)
		target.snowman = null
		target.actor.setTimeScale(1.)
		target.actor.getOwner().selectSingle(target.actor)


@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("Santa magic inside.")
	new ChannelAbilityPreset(SNOWMAN_SPELL_ID, 4, true, tgen)
	..presetTargetTypes(Targettype.POINT)
	..setRequiredLevel(3)
	..presetButtonPosNormal(2, 1)
	..tooltipStartListen()
	..presetHotkey("F")
	..setName("Snowman missile")
	..presetIcon("BTNFrostSpell")
	..presetCooldown(lvl -> 18.5 - (lvl * 0.5))
	..presetManaCost(lvl -> 6 + (lvl))

	..tooltipStopListen()
