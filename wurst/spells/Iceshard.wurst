package Iceshard
import Projectile
import Fx
import initlater Child
import Assets
import Collision
import AbilityTooltipGenerator
import DummyCaster
import Orders

public constant ICESHARD_SPELL_ID = compiletime(ABIL_ID_GEN.next())
public constant NOVA_DUMMY_SPELL_ID = compiletime(ABIL_ID_GEN.next())
public constant NOVA_DUMMY_SPELL_PID = compiletime(ABIL_ID_GEN.next())

constant real SPEED = 25.75

public class Iceshard extends PhysicsProjectile
	var initialZ = 0.
	var lvl = 1

	construct(vec3 pos, player owner, angle angle, int lvl)
		super(pos, 28., owner, angle, Abilities.lichMissile)
		this.lvl = lvl
		fx..setOwner(owner, false)
		..setScale(0.75)
		setTimed(1.35)
		setSpeed(SPEED)
		surfaceFriction = 1
		initialZ = pos.getTerrainZ() + 18.
		registerCollidable(this, (other) -> onHit(other))
		registerCollider(this, SHAPE_SPHERE, (other) -> onHit(other))

	override function update()
		super.update()
		if pos.getTerrainZ() > initialZ
			flashEffect(Abilities.lichMissile, pos)
			terminate()


	function onHit(Entity e)
		if e.owner.isEnemyOf(owner)
			if e instanceof Child
				if (e castTo Child).actor.isAlive()
					var c = e castTo Child
					flashEffect(Objects.critterBloodAlbatross, e.getPos())
					flashEffect(Objects.critterBloodAlbatross, e.pos + vec3(0, 0, 12))
					fx.getDummy().damageTarget(c.actor, 21 + lvl * 4., ATTACK_TYPE_PIERCE)
					new DummyCaster()
					..owner(owner)
					..origin(getPos().toVec2())
					..castTarget((c.actor.hasAbility('Bfro') or c.hasPresent) ? NOVA_DUMMY_SPELL_PID : NOVA_DUMMY_SPELL_ID, 1, OrderIds.frostnova, c.actor)
					terminate()
			else
				terminate()

	ondestroy
		flashEffect(Abilities.frostArmorDamage, pos)
		removeCollidable(this)
		removeCollider(this)


@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("Small, fast ice projectile that slows on hit.\nLevel 3 and up throw three shards.")
	new ChannelAbilityPreset(ICESHARD_SPELL_ID, 4, true, tgen)
	..presetButtonPosNormal(3, 2)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("R")
	..setName("Ice Shard")
	..presetIcon("BTNIceShard")
	..presetCooldown(lvl -> 2.0 + lvl * 0.25)
	..presetManaCost(lvl -> 3 - (lvl / 3).toInt())
	..tooltipStopListen()

	new AbilityDefinitionLichFrostNova(NOVA_DUMMY_SPELL_ID)
	..setHeroAbility(false)
	..presetManaCost(lvl -> 0)
	..presetCastRange(lvl -> 999999.)
	..presetCooldown(lvl -> 0)
	..setArtEffect("")
	..presetSpecificTargetDamage(lvl -> 1)
	..presetAreaofEffectDamage(lvl -> 1.)
	..presetAreaofEffect(lvl -> 8.)
	..presetDurationHero(lvl -> 2.5)

	new AbilityDefinitionLichFrostNova(NOVA_DUMMY_SPELL_PID)
	..setHeroAbility(false)
	..presetManaCost(lvl -> 0)
	..presetCastRange(lvl -> 999999.)
	..presetCooldown(lvl -> 0)
	..setArtEffect("")
	..presetSpecificTargetDamage(lvl -> 1)
	..presetAreaofEffectDamage(lvl -> 1.)
	..presetAreaofEffect(lvl -> 8.)
	..presetDurationHero(lvl -> 0.25)
